apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.launcher.mango"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode Integer.parseInt(rootProject.versionCode)
        versionName rootProject.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
            }
            proto {
                srcDirs 'src/main/proto'
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation deps.test.junit
    androidTestImplementation deps.test.uiautomator
    androidTestImplementation deps.test.mockito
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.espresso
    androidTestImplementation deps.support.annotations
    androidTestImplementation deps.google.dexmaker
    androidTestImplementation deps.google.dexmaker_mockito

    implementation deps.support.app_compat
    implementation deps.support.v4
    implementation deps.support.preference_v7
    implementation deps.support.preference_v14
    implementation deps.support.palette
    implementation deps.support.recyclerview
    implementation deps.support.constraint_layout
    implementation deps.support.dynamic_animation

    implementation "com.google.protobuf.nano:protobuf-javanano:3.2.0rc2"
    implementation deps.google.guava

    implementation deps.rx.android
    implementation deps.rx.java
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option "java_package=launcher_log_extension.proto|com.launcher.mango.userevent.nano"
                        option "java_package=launcher_log.proto|com.launcher.mango.userevent.nano"
                        option "java_package=launcher_dump.proto|com.launcher.mango.model.nano"
                        option "enum_style=java"
                    }
                }
            }
        }
    }
}
